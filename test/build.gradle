apply plugin: "cpp"
apply plugin: "google-test"

apply from: "../googletest/platform-config.gradle"

def prebuiltPath = "../publish"

def libName(def binary, def name) {
    binary.targetPlatform.operatingSystem.windows ? "${name}.lib" : "lib${name}.a"
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            gtest {
                headers.srcDir "${prebuiltPath}/include"
                binaries.withType(StaticLibraryBinary) { binary->
                    staticLibraryFile = file("${prebuiltPath}/lib/gtest/static/${binary.targetPlatform.name}/${libName(binary, 'gtest')}")
                }
            }
        }
    }
    components {
         operators(NativeLibrarySpec)
    }
    binaries {
        withType(TestSuiteBinarySpec) {
            lib library: "gtest", linkage: "static"

            if (toolChain in Gcc) {
                linker.args "-pthread"
            }
        }
    }
    tasks {
        unitTest(Task) {
            dependsOn $.binaries.withType(NativeTestSuiteBinarySpec)
                .findAll { it.buildable }
                // TODO: This should be it.tasks.run, which should use the install image of the test exe
                .collect { it.namingScheme.getTaskName("run") }
            group "Verification"
        }
    }
}

