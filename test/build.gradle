apply plugin: "cpp"
apply plugin: "google-test"

apply from: "../googletest/platform-config.gradle"

def prebuiltPath = "../publish"

def libName(def binary, def name) {
    binary.targetPlatform.operatingSystem.windows ? "${name}.lib" : "lib${name}.a"
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            gtest {
                headers.srcDir "${prebuiltPath}/include"
                binaries.withType(StaticLibraryBinary) { binary->
                    staticLibraryFile = file("${prebuiltPath}/lib/gtestStaticLibrary/${binary.targetPlatform.name}/${libName(binary, 'gtest')}")
                }
            }
            gtest_main {
                headers.srcDir "${prebuiltPath}/include"
                binaries.withType(StaticLibraryBinary) { binary ->
                    staticLibraryFile = file("${prebuiltPath}/lib/gtest_mainStaticLibrary/${binary.targetPlatform.name}/${libName(binary, 'gtest_main')}")
                }
            }
        }
    }
    components {
         operators(NativeLibrarySpec)
    }
}

binaries.withType(TestSuiteBinarySpec) {
    lib library: "gtest_main", linkage: "static"
}

task unitTest

binaries.withType(TestSuiteBinarySpec) {
    if (it.buildable) {
        println "Adding dependency on $it"
        // TODO: This should be it.tasks.run, which should use the install image of the test exe
        unitTest.dependsOn it.namingScheme.getTaskName("run")
    }
}